plugins {
    id 'java'
    id 'io.freefair.lombok' version '5.1.0'
    id 'org.beryx.runtime' version '1.8.4'
}

sourceCompatibility = 14
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}
sourceSets.main.java.srcDirs = [ "src/" ]

mainClassName = "com.marvelousbob.server.BobServerLauncher"

task dist(type: Jar) {
    project.version="1" // adjust the CI/CD if you change this
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar

    destinationDirectory = file("$buildDir/lib")
}

jpackageImage.dependsOn dist
dist.dependsOn classes

// JLink configuration to minimize size of generated jar
runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base' ,
               'java.desktop',
               'jdk.unsupported']
    distDir = file(buildDir)

    jpackage {
        //jpackageHome = '/usr/lib/jvm/open-jdk'
        mainJar = dist.archiveFileName.get()
    }
}

//repositories {
//    mavenCentral()
//}
//dependencies {
//    testImplementation group: 'junit', name: 'junit', version: '4.12'
//}
//test {
//    useJUnitPlatform()
//}